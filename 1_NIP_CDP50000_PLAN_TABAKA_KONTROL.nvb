' Tarih     : 30.09.2015
' Açıklama  :
'           [+] 2017
'           [+]     NCZ'de olupda şablonda karşılığı olmayan tabakaların raporlanması sağlandı
'           [+] 20150101.v2
'           [+]     Birden fazla obje tipine sahip olan tabakalardaki hatalı tasarım düzeltildi
'           [+]     Performans artışı için değişiklikler yapıldı
'           [+]     Netcad kurulum dizininde bulunan Logs klasörüne raporların oluşturulması sağlandı (W7'de yetkiye takılıyordu)
'           [+]     Arayüzdeki yazım hataları düzeltildi
'           [+]     Versiyon bilgisi arayüze eklendi
'           [+]     İşlem ilerlemesini gösteren loaderbar eklendi
'           [+]     ESC tuşu ile işlemin yarıda kesilmesi sağlandı
'           [+]     ŞABLON dosya bilgisinin boş geçilmesi durumunda işlem devam edilmemesi sağlandı
'           [+]     ŞABLON dosya var olup olmadığı kontrol edilerek işleme devam ediliyor
'           [+]     CSV'deki "Metni Sütünlara Dönüştür" oluşan hata giderildi
'           [+]     Rapor içeriğine sıra no eklendi
'           [+] 20150930.v1
'           [+]     Karayolları KVP için NCZ dosyalarının excel şablona göre kontrol eder.
'           [+]     Bulunamayan tabakaları CSV olarak raporlar
'           [+]     Tabakalardaki obje standartlarını kontrol eder ve raporlar
'###############################################################################################################################
public hedef_tabaka_adi,hedef_obje_turu,YB,nczFileName,nowDate,nc
Const ForReading = 1, ForWriting = 2, ForAppending = 8
'##############################################################################################################################
Sub Main()
dim i,objExcel,objWorkbook,intRow,objFSO,tabakaCSV,objeCSV,objArr,objAra,objeSay,objType,BD,excelFile,count,lyrName
dim satirsay,tabakaCSV2,durumSay
with Netcad
    .ClosePercent
    nc = .GetParam(PNC_NETCADDIR)
    nczFileName = DosyaAdiniDuzelt(.GetParam(PNC_CURRENTFILE))
    nowDate = Year(now) & Month(now) & Day(now)
    ' // =======================================================================================================================
    set BD = Netcad.NewBDialog("YIGM PL Tabaka Kontrol Sihirbazı :::2017")
    BD.PutPrompt "İşlem Yapılacak Dosya : "
    BD.PutPrompt .GetParam(PNC_CURRENTFILE) & " "
    BD.PutPrompt " "
    BD.GetFileName "item7","Standart Tabaka Listesi","\\10.0.0.230\yigmgis\MAKRO\TABAKA_KONTROL\2_NIP_CDP50000_PLAN_TABAKA_KONTROL.xls","Excel Dosyalari|*.XLS|Tum Dosyalar|*.*","XLS"
    BD.PutPrompt "  - Standart Tabaka Listesi dosyasını değiştirmeyiniz! "
    BD.PutPrompt "NOT : "
    BD.PutPrompt "  - Netigma'ya yeni bir tabaka ismi tanımladıysanız Standart Tabaka Listesinin"
    BD.PutPrompt "    güncellenmesi için Netcad Portala talep açınız,"
    BD.PutPrompt "  - Raporlar Netcad kurulum dizini altındaki \Logs\ klasöründedir,"
    BD.PutPrompt "  - Projedeki Açık / Kapalı tüm PL tabakaları kontrol edilir,"
    BD.PutPrompt " "
    BD.PutPrompt "Netcad Yazılım A.Ş."
    if BD.showmodal then
        excelFile = BD.ValueByName("item7")
        if IsNull(excelFile) or excelFile="" then
            .ClosePercent
            msgbox "Standart Tabaka Listesi. Komutu kapatıp baştan çalıştırabilirsiniz" & Chr(13) & "Devam edilmeyecek",0+16,"BİLGİ"
            exit sub
        end if
        .FindWorld
    else
        .ClosePercent
        msgbox "İşlemi İptal Ettiniz." & Chr(13) & "Devam edilmeyecek",0+16,"BİLGİ"
        exit sub
    end if
    set BD = Nothing
    ' // =======================================================================================================================
    ' // Excel okunuyor
    ' // =======================================================================================================================
    .ClosePercent
    .ShowPercent 0,100,0,"Standart Tabaka Listesi Açılıyor. Lütfen Bekleyiniz..."
    Set objFSO = CreateObject("Scripting.FileSystemObject")
    If (objFSO.FileExists(excelFile)) Then
    else
        msgbox "Standart Tabaka Listesi Bulunamadı.Komutu kapatıp baştan çalıştırabilirsiniz." & Chr(13) & "Devam edilmeyecek",0+16,"BİLGİ"
        .ClosePercent
        exit sub
    End If
    Set objExcel = CreateObject("Excel.Application")
    Set objWorkbook = objExcel.Workbooks.Open (excelFile)
    intRow = 2
    ' // =======================================================================================================================
    ' // NCZ'de var olan tabakanın ŞABLON'daki karşılığını kontrol et
    ' // =======================================================================================================================
    Set tabakaCSV2 = objFSO.CreateTextFile(nc & "logs\PLANKONTROL_" & nowDate & "_" & nczFileName & "_UYUMSUZPLTABAKA.csv", ForWriting, True)
    satirsay = 0
    for i=1 to .NumLayers-1
        lyrName = .LayerNameOf(i)
        .UpdatePercentX 30,lyrName & "->Uyumsuz PL Tabaka Kontrolü Yapılıyor. Lütfen Bekleyiniz..."
        intRow = 2 : durumSay = 0
        Do Until objExcel.Cells(intRow,1).Value = ""
            if objExcel.Cells(intRow,1).Value=lyrName then
                durumSay = 0
                exit do
            else
                durumSay = durumSay + 1
            end if
            intRow = intRow + 1
            if .EscPressed then
                msgbox "İşlemi İptal Ettiniz",0+16,"BİLGİ"
                .ClosePercent
                .FindWorld
                exit sub
            end if
        Loop
        dim filesem
		filesem = split(lyrName,"_")
		'msgbox filesem(0)
       if (filesem(0) = "PL") then
			if (durumSay>0) then
				satirsay = satirsay + 1
				tabakaCSV2.Write satirsay & ";" & lyrName & ";Tabakası Standart Değildir. Sayısallaştırma menüsü ile ilişkilendirme yapabilir ya da tabakanın ismini Netigma'dan kontrol ederek değiştiriniz;"
				tabakaCSV2.Writeline
			end if
		end if
    next
   
    msgbox "Uyumsuz PL Tabaka kontrolü yapıldı. Şimdi PL tabakalarındaki alan objesi dışındaki objelerin kontrolü yapılacak" & Chr(13) & " Sonuç rapor : " & nc & "logs\PLANKONTROL_" & nowDate & "_" & nczFileName & "_UYMSUZ_PL_TABAKA.csv",0+64,"BİLGİ"
    set tabakaCSV2 = nothing
    ' // =======================================================================================================================
    ' // Tabakadaki obje tiplerini kontrol et
    ' // =======================================================================================================================
    Set objeCSV = objFSO.CreateTextFile(nc & "logs\YIGM_" & nowDate & "_" & nczFileName & "_UYUMSUZ_OBJETURLERI.csv", ForWriting, True)
    .UpdatePercentX 40,"PL Tabakalarındaki Alan Dışı Objelerin Kontrolü İçin CSV Oluşturuluyor. Lütfen Bekleyiniz..."
    intRow = 2 : satirsay = 0
    Do Until objExcel.Cells(intRow,1).Value = ""
        .UpdatePercentX 75,objExcel.Cells(intRow,1).Value & "->TABAKA İçin OBJE Tipi Kontrolü. Lütfen Bekleyiniz..."
        if .FoundLayer(objExcel.Cells(intRow,1).Value)<>-1 and objExcel.Cells(intRow,2).Value<>"" then ' // Tabaka var ise objeleri kontrol et
            count = 0
            set objAra = .newobject()
            .setFilter nothing,array(.FoundLayer(objExcel.Cells(intRow,1).Value)),array()
            while .GetNextObject2(objAra)
                .DrawObject objAra, blue
                objType = ObjeTipi(objAra)
                objArr = Split(objExcel.Cells(intRow,2).Value & ";",";")
                objeSay = 0
                for i=0 to UBound(objArr)-1
                    if objArr(i)="ALAN" then
                        if (objType = "T.ALAN" or objType = "TR.ALAN") then objType = "ALAN"
                    end if
                    if (objType=objArr(i)) then
                        objeSay = 0
                        exit for
                    else
                        objeSay = 1
                    end if
                next
                if (objeSay>0) then count = count + 1
                if .EscPressed then
                    msgbox "İşlemi İptal Ettiniz",0+16,"BİLGİ"
                    .ClosePercent
                    .FindWorld
                    exit sub
                end if
            wend
            .ResetFilter
            set objAra = nothing
            if (count>0) then
                satirsay = satirsay + 1
                objeCSV.Write satirsay & ";" & objExcel.Cells(intRow,1).Value & ";" & Replace(objExcel.Cells(intRow,2).Value,";"," + ") & ";Geometrisine Sahip Olmayan ;" & count & " Adet Obje"
                objeCSV.Writeline
            end if
        end if
        intRow = intRow + 1
    Loop
   .UpdatePercentX 90,"PL Tabakalarındaki Alan Dışı Objelerin Kontrolü Tamamlandı. Lütfen Bekleyiniz..."
    msgbox "PL Tabakalarındaki Alan Dışı Objelerin Kontrolü Yapıldı" & Chr(13) & " Sonuç rapor : " & nc & "logs\YIGM_" & nowDate & "_" & nczFileName & "_UYUMSUZ_OBJETURLERI.csv",0+64,"BİLGİ"
    set objeCSV = nothing
    objExcel.Quit
    ' // =======================================================================================================================
   .UpdatePercentX 100,"İşlem Tamamlandı. Lütfen Bekleyiniz..."
    msgbox "İşlem Tamamlandı. Rapor Dosyalarına Netcad Kurulum Dosyası Altındaki Logs Klasöründen Ulaşabilirsiniz. Aynı Plan İçin Tekrar Kontrol İşlemini Çalıştıracaksanız Oluşan Rapor Dosyalarının Kapalı Olmasına Dikkat Ediniz ",0+64,"BİLGİ"
    .ClosePercent
    .FindWorld
    ' // =======================================================================================================================
    set objExcel = nothing
    set objWorkbook = nothing
    set objFSO = nothing
end with
end sub
'##############################################################################################################################
function DosyaAdiniDuzelt(fileName)
dim i,fileNameArr,trDuzelt,chrDuzelt
    fileNameArr = split(UCase(fileName),"\")

    DosyaAdiniDuzelt = fileNameArr(UBound(fileNameArr))
    DosyaAdiniDuzelt = LTrim(DosyaAdiniDuzelt)
    DosyaAdiniDuzelt = RTrim(DosyaAdiniDuzelt)
    DosyaAdiniDuzelt = Trim(DosyaAdiniDuzelt)

    trDuzelt = array ("Ç","C","Ö","O","Ş","S","Ğ","G","Ü","U","İ","I","ç","C","ö","O","ş","S","ğ","G","ü","U","i","I")
    for i = 0 to UBound(trDuzelt)-1 step 2
        DosyaAdiniDuzelt = Replace(DosyaAdiniDuzelt,trDuzelt(i),trDuzelt(i+1))
    next

    chrDuzelt = array ( ".NCZ","", ",","", "_","", "/","", "\","", "?","", "#","", "+","", ";","", "(","", ")","", "[","", _
                        "]","", "{","", "}","", "'","", "=","", "~","")
    for i = 0 to UBound(chrDuzelt)-1 step 2
        DosyaAdiniDuzelt = Replace(DosyaAdiniDuzelt,chrDuzelt(i),chrDuzelt(i+1))
    next
end function
'##############################################################################################################################
function ObjeTipi(objType)
dim i
with netcad
    ObjeTipi = ""
    select case objType.Tag
    case   0 : 'odeleted   =  0
        ObjeTipi = "SILINDI"
    case   1 : 'opoint     =  1
        ObjeTipi = "NOKTA"
    case   2 : 'oline      =  2
        ObjeTipi = "DOGRU"
    case   3 : 'ocircle    =  3
        ObjeTipi = "DAIRE"
    case   4 : 'oarc       =  4
        ObjeTipi = "YAY"
    case   5 : 'otext      =  5
        ObjeTipi = "YAZI"
    case   6 : 'oshape     =  6
        ObjeTipi = "SEMBOL"
    case   7 : 'opline     =  7
        if objType.Area = 0 then
            ObjeTipi = "C.DOGRU"
        else
            if objType.Flags=19 then ObjeTipi = "TR.ALAN" '16+2+1
            if objType.Flags=18 then ObjeTipi = "TR.ALAN" '16+2
            if objType.Flags=3 then ObjeTipi = "TR.ALAN" '2+1
            if objType.Flags=17 then ObjeTipi = "T.ALAN" '16+1
            if objType.Flags=1 then ObjeTipi = "ALAN" '1
        end if
    case   8 : 'ospiral    =  8
        ObjeTipi = "SPIRAL"
    case   9 : 'oizohdr    =  9
        ObjeTipi = "EGRI"
    case  10 : 'orectangle = 10
        ObjeTipi = "KUTU"
    case  11 : 'ostpafta   = 11
        ObjeTipi = "STPAFTA"
    case  12 : 'otriang    = 12
        ObjeTipi = "UCGEN"
    case  13 : 'oblock     = 13
        ObjeTipi = "BLOK"
    case  14 : 'omark      = 14
        ObjeTipi = "MARK"
    end select
end with
end function

